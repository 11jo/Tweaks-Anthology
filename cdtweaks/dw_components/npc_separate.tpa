DEFINE_ACTION_FUNCTION npc_separate BEGIN


   ACTION_IF SFO_lite BEGIN
      LAM read_in_NPC_dialogs
   END

   ACTION_IF GAME_IS ~eet~ BEGIN
      LAM npc_separate_EE
   END
   <<<<<<<<    .../stratagems-inline/npc_data.2da
   entry                  bgee          tutu           bgt                 eet
   jaheira_loc            5             0              78                  5
   khalid_loc             5             1              5                   5
   minsc_loc              4             0              14                  4
   dynaheir_loc           4             0              4                   4
   montaron_loc           5             1              5                   5
   xzar_loc               5             1              5                   5
   eldoth_loc             4             0              4                   4
   skie_loc               4             0              4                   4
   jaheira_stay           6             6              999                 6
   khalid_stay            6             6              6                   6
   minsc_stay             999           999            13                  999
   dynaheir_stay          6             6              6                   6
   montaron_stay          7             7              7                   7
   xzar_stay              6             6              6                   6
   eldoth_stay            8             8              8                   8
   skie_stay              6             6              6                   6
   kicked_out_variable    KickedOut     KickedOut      IWasKickedOut       KickedOut
   >>>>>>>>


      ACTION_IF GAME_IS bgee BEGIN
           OUTER_SPRINT read_array_column bgee
      END ELSE
      ACTION_IF GAME_IS bgt BEGIN
           OUTER_SPRINT read_array_column bgt
      END ELSE
      ACTION_IF GAME_IS ~tutu tutu_totsc~ BEGIN
           OUTER_SPRINT read_array_column tutu
      END ELSE
      ACTION_IF GAME_IS eet BEGIN
           OUTER_SPRINT read_array_column eet
      END ELSE BEGIN
         FAIL "unidentified game at crossplatform"
      END
      OUTER_SPRINT array_to_read npc_data.2da
      OUTER_SPRINT read_array_is_inline yes
      LAM read_in_variables
   COMPILE EVALUATE_BUFFER ~%scsroot%/%component_loc%/resource/pairsep.d~

   ACTION_CLEAR_ARRAY npc_data
   ACTION_DEFINE_ASSOCIATIVE_ARRAY npc_data BEGIN
      KHALID=>jaheira
      JAHEIRA=>khalid
      XZAR=>montaron
      MONTARON=>xzar
      ELDOTH=>skie
      SKIE=>eldoth
      MINSC=>dynaheir
      DYNAHEIR=>minsc
   END
   

   // the point of this bit of the code is 
   //
   // (1) that the dialog file checks if the "partner" NPC is alive before offering a double join. On this version, it needs to check if they're
   // alive, not in the party, and in the area.
   //
   // Mostly, we can swap "Dead(x)" for "OR(3)Dead(x)InParty(x)!InMyArea(x)" and "!Dead(x)" for "!Dead(x)!InParty(x)InMyArea(x)"
   //
   // Minsc is a special case, because Dynaheir and he join at different locations and so might not both be in the party initially. Only EE supports this properly,
   // via the BeenInParty check. On unmodded EE, the possibilities are "OR(2)Dead(x)!BeenInParty(x)" and "!Dead(x)BeenInParty(x)".
   //
   //
   // (2) that when we kick the ally out via an ActionOverride(x,LeaveParty()), we also issue an ActionOverride(x,SetGlobal("kickedoutvariable","LOCALS",1)))


   ACTION_PHP_EACH npc_data AS script=>ally BEGIN
      OUTER_SPRINT scriptfile EVALUATE_BUFFER ~%%script%_POSTJOIN%~
      ACTION_TO_LOWER script
      OUTER_SPRINT staynum EVALUATE_BUFFER ~%%script%_stay%~
      ACTION_MATCH "%scriptfile%%enhanced_edition%" WITH
      "%MINSC_POSTJOIN%1" BEGIN
          LAF swap_text STR_VAR files="%scriptfile%.dlg" swaps=~OR(2)=>OR(4) !Dead("%ally%")=>NOTDEADMARKER Dead("%ally%")=>Dead("%ally%")InParty("%ally%")!InMyArea("%ally%")  NOTDEADMARKER=>InMyArea("%ally%")!InParty("%ally%")!Dead("%ally%")~ END
      END
      DEFAULT
          LAF swap_text STR_VAR files="%scriptfile%.dlg" swaps=~!Dead("%ally%")=>NOTDEADMARKER Dead("%ally%")=>OR(3)Dead("%ally%")InParty("%ally%")!InMyArea("%ally%")  NOTDEADMARKER=>InMyArea("%ally%")!InParty("%ally%")!Dead("%ally%")~ END
      END
      LAF swap_text STR_VAR files="%scriptfile%.dlg" swaps=~ActionOverride("%ally%",LeaveParty())=>ActionOverride("%ally%",LeaveParty())ActionOverride("%ally%",SetGlobal("%kicked_out_variable%","LOCALS",1))~ END
      LAF swap_text STR_VAR files="%scriptfile%.dlg" swaps=~ActionOverride("%ally%",JoinParty())=>ActionOverride("%ally%",JoinParty())ActionOverride("%ally%",SetGlobal("%kicked_out_variable%","LOCALS",0))~ END
      ACTION_IF !(staynum=999) BEGIN
         <<<<<<<< pairsep2.d
         ALTER_TRANS ~%scriptfile%~ BEGIN ~%staynum%~ END BEGIN 0 END BEGIN "ACTION" "" END
         >>>>>>>>
         COPY pairsep2.d "%workspace%" EVALUATE_BUFFER
         COMPILE "%workspace%/pairsep2.d"
      END

   END




END

DEFINE_ACTION_MACRO npc_separate_EE BEGIN //  because EET hot-generates the pdialog folder late in the install order, the automated code doesn't pick up the %postjoin% variables.
                                          // Set them manually. (This therefore needs to be a macro, not a function.)
   ACTION_CLEAR_ARRAY patch_data
   ACTION_DEFINE_ASSOCIATIVE_ARRAY patch_data BEGIN
        DYNAHEIR=>DYNAHP
        MINSC=>MINSCP_
        XZAR=>XZARP
        MONTARON=>MONTAP
        JAHEIRA=>JAHEIP
        KHALID=>KHALIP
        ELDOTH=>ELDOTP
        SKIE=>SKIEP
   END
   ACTION_PHP_EACH patch_data AS name=>file BEGIN
      OUTER_SPRINT "%name%_POSTJOIN" "%file%"
   END
END


