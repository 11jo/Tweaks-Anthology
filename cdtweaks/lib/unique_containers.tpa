/////                                                  \\\\\
///// heavy lifting                                    \\\\\
/////                                                  \\\\\

// replaces all of the t-foo_icons and t-foo_name libraries

ACTION_PHP_EACH cd_unique_containers AS id => item BEGIN

  ACTION_IF ((FILE_EXISTS_IN_GAME ~%item%.itm~) AND (FILE_EXISTS_IN_GAME ~%item%.sto~)) BEGIN
    
    ACTION_IF id < 200 BEGIN                          // gem bags (100s)
      OUTER_SET    idic = (id - 100)
      OUTER_SPRINT icon ~t-bgem~
    END ELSE BEGIN
      ACTION_IF ((id > 199) AND (id < 300)) BEGIN     // scroll case (200s)
        OUTER_SET    idic = (id - 200)
        OUTER_SPRINT icon ~t-bscr~
      END ELSE BEGIN
        ACTION_IF ((id > 299) AND (id < 400)) BEGIN   // bags of holding (300s)
          OUTER_SET    idic = (id - 300)
          OUTER_SPRINT icon ~t-bhld~
        END ELSE BEGIN
          ACTION_IF ((id > 399) AND (id < 500)) BEGIN // ammo belts (400s)
            OUTER_SET    idic = (id - 400)
            OUTER_SPRINT icon ~t-bamm~
          END ELSE BEGIN                              // potion cases (500s)
            OUTER_SET    idic = (id - 500)
            OUTER_SPRINT icon ~t-bptn~
          END
        END
      END
    END
    
    ACTION_IF ((id != 119) AND (id != 218) AND (id != 336) AND (id != 413) AND (id != 516)) BEGIN // original bags (bag02, bag03, etc.)

      COPY ~cdtweaks/bam/%icon%%idic%.bam~ ~override~

      COPY_EXISTING ~%item%.itm~ ~override~
        PATCH_IF SOURCE_SIZE > 0x71 BEGIN
          WRITE_ASCIIE 0x3a ~%icon%%idic%~ #8 //Inventory icon
        END
        BUT_ONLY

    END
      
    ACTION_IF names = 1 BEGIN
    
      OUTER_SET id += 18000
    
      COPY ~cdtweaks/lib/unique_containers_name.tpa~ ~cdtweaks/lib/unique_containers_working.tpa~
        REPLACE_TEXTUALLY ~REPLACE_ME~ ~%id%~
      
      REINCLUDE ~cdtweaks/lib/unique_containers_working.tpa~
    
    END
  
  END

END
