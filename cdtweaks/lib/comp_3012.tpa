/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Maximum HP for NPCs                              \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

/////                                                  \\\\\
///// Maximum HP for Party-Joinable NPCs               \\\\\
/////                                                  \\\\\

INCLUDE ~cdtweaks/lib/max_hp_creatures.tpa~ // macro max_hp_creatures will be different for iwd2 vs. other games

INCLUDE ~cdtweaks/lib/joinable_npc_array.tpa~

ACTION_PHP_EACH JOINABLE_NPC_ARRAY AS file => dv BEGIN

  ACTION_IF !IS_AN_INT dv BEGIN // joinable NPC

    COPY_EXISTING ~%file%~ ~override~
      PATCH_IF (NOT ~%SOURCE_RES%~ STRING_EQUAL_CASE ~riftcr04~) BEGIN // skip creatures meant to have lower hp
        READ_SHORT 0x24 "currenthp" ELSE 0 // fortunately all cre formats keep HP info at these offsets
        READ_SHORT 0x26 "maxhp"     ELSE 0
        PATCH_IF (("%maxhp%" > 0) AND ("%currenthp%" > 0)) BEGIN
          LAUNCH_PATCH_MACRO ~max_hp_creatures~ // contains rest of patch
        END
      END
      BUT_ONLY
      
  END

END  

ACTION_IF !game_is_pst AND !game_is_pstee BEGIN // no character files on these games

  ACTION_BASH_FOR ~%USER_DIRECTORY%/characters~ ~^.+\.chr$~ BEGIN
  
    COPY ~%BASH_FOR_FILESPEC%~ ~%BASH_FOR_FILESPEC%~
      LAUNCH_PATCH_MACRO ~max_hp_creatures~ // contains rest of patch, including validity checks
      BUT_ONLY
  
  END
  
END