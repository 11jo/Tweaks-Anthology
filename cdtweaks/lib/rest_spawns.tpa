OUTER_SET alleg_off = 0x270 // cre v1.0
OUTER_SET rest_offr = 0x0c0 // cre v1.0

ACTION_IF GAME_IS ~pst~ BEGIN
  OUTER_SET alleg_off = 0x314 // cre v1.2
END

ACTION_IF GAME_IS ~iwd how totlm~ BEGIN
  OUTER_SET alleg_off = 0x2d8 // cre v9.0
END

ACTION_IF GAME_IS ~iwd2~ BEGIN
  OUTER_SET alleg_off = 0x384 // cre v2.2
  OUTER_SET rest_offr = 0x0d0 // are v9.1
END

COPY_EXISTING_REGEXP GLOB ~^.+\.are$~ ~override~
//  SPRINT area_name "%SOURCE_RES%"
  PATCH_IF alleg_off = 0x314 BEGIN // pst
    WRITE_BYTE 0x14 (THIS | BIT1) // this bit is called "Tutorial" in NI, but is really the can_not_sleep one.
  END ELSE BEGIN
    WRITE_BYTE 0x48 (THIS | BIT7)
  END
  SET proceed = 0
  READ_LONG rest_offr rest_off
  PATCH_IF check_spawns = 1 BEGIN
    SET proceed = 1
  END ELSE
  PATCH_IF check_spawns > 1 BEGIN
    SET hostile = 3
    FOR (index = 0 ; index < 10 ; ++index) BEGIN
      READ_ASCII (rest_off + 0x48 + (index * 0x08)) cre
      PATCH_IF (("%cre%" STRING_COMPARE_CASE "") AND ("%cre%" STRING_COMPARE_CASE "None")) BEGIN
//        PATCH_PRINT ~checking %cre% in %area_name%~
        PATCH_IF (FILE_CONTAINS_EVALUATED(~spawngrp.2da~ ~\b%cre%\b~)) BEGIN // if from spawngrp, assume hostile
          SET hostile = 2
          SET index = 10 // kill loop
        END ELSE BEGIN
          PATCH_IF FILE_EXISTS_IN_GAME ~%cre%.cre~ BEGIN
            INNER_ACTION BEGIN

              COPY_EXISTING ~%cre%.cre~ ~override~
                READ_BYTE alleg_off allegiance
                PATCH_IF (allegiance = 255) BEGIN
                  SET hostile = 2
                  SET index = 10 // kill loop
                END
                BUT_ONLY

            END // inner action
          END // file check
        END // spawngrp else if
      END // cre name check
    END // for loop
    PATCH_IF (hostile = check_spawns) BEGIN SET proceed = 1 END
  END // check_spawns
  PATCH_IF (proceed = 1) BEGIN
//    PATCH_PRINT ~patching %area_name%~
    WRITE_LONG rest_off + 0xa4 0 // make inactive, set max creatures to 0, and
    WRITE_LONG rest_off + 0xa8 0 // drop probabilities to zero for good measure
  END // check spawns
  BUT_ONLY