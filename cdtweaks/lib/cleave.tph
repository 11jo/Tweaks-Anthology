DEFINE_ACTION_FUNCTION "CLEAVE"
BEGIN
	<<<<<<<< .../cdtweaks-inlined/empty
	>>>>>>>>
	//
	CREATE "eff" "cdcleave"
	COPY_EXISTING "cdcleave.eff" "override"
		WRITE_LONG 0x10 402 // invoke lua
		WRITE_SHORT 0x2C 100 // prob1
		WRITE_ASCII 0x30 "GTCLV01" #8 // lua function
	BUT_ONLY
	//
	CREATE "spl" "cdcleave"
	COPY_EXISTING "cdcleave.spl" "override"
		WRITE_LONG NAME1 RESOLVE_STR_REF (@0)
		WRITE_LONG 0x18 BIT14 // flags: ignore dead/wild magic
		WRITE_SHORT 0x1C 4 // type: innate
		WRITE_LONG 0x34 1 // level
		//
		LPF "ADD_SPELL_HEADER" INT_VAR "range" = 30 END
		//
		LPF "ADD_SPELL_EFFECT" INT_VAR "opcode" = 138 "target" = 1 "timing" = 1 END // Set animation sequence (ATTACK) -- probably not needed...?
		LPF "ADD_SPELL_EFFECT" INT_VAR "opcode" = 402 "target" = 2 STR_VAR "resource" = "GTCLV02" END // invoke lua
	BUT_ONLY_IF_IT_CHANGES
	// Listener: run 'func' each time a sprite has finished evaluating its effects
	ACTION_IF !(FILE_EXISTS_IN_GAME "m_gtlstn.lua") BEGIN
		COPY ".../cdtweaks-inlined/empty" "override\m_gtlstn.lua"
			DELETE_BYTES 0x0 BUFFER_LENGTH
			INSERT_BYTES 0x0 STRING_LENGTH "-- Listeners --%WNL%%WNL%"
			WRITE_ASCII 0x0 "-- Listeners --%WNL%%WNL%"
		BUT_ONLY_IF_IT_CHANGES
	END
	COPY_EXISTING "m_gtlstn.lua" "override"
		APPEND_FILE TEXT "cdtweaks\luke\lua\cleave_grant.lua"
	BUT_ONLY UNLESS "cdtweaksCleave"
	//
	ACTION_IF !(FILE_EXISTS_IN_GAME "m_gt#402.lua") BEGIN
		COPY ".../cdtweaks-inlined/empty" "override\m_gt#402.lua"
			DELETE_BYTES 0x0 BUFFER_LENGTH
			INSERT_BYTES 0x0 STRING_LENGTH "-- Functions to be invoked via op402 --%WNL%%WNL%"
			WRITE_ASCII 0x0 "-- Functions to be invoked via op402 --%WNL%%WNL%"
		BUT_ONLY_IF_IT_CHANGES
	END
	COPY_EXISTING "m_gt#402.lua" "override"
		APPEND_FILE TEXT "cdtweaks\luke\lua\cleave.lua"
	BUT_ONLY UNLESS "^function GTCLV0[1-2]"
	//
	ACTION_IF !(FILE_EXISTS_IN_GAME "m_gttbls.lua") BEGIN
		COPY "cdtweaks\luke\lua\m_gttbls.lua" "override"
	END
END