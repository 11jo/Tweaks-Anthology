DEFINE_ACTION_FUNCTION "DIVINE_GRACE_DARK_BLESSING"
BEGIN
	<<<<<<<< .../cdtweaks-inlined/empty
	>>>>>>>>
	//
	ACTION_IF !(FILE_EXISTS_IN_GAME "gtdvngrc.bam") BEGIN
		COPY "cdtweaks\bam\gtdvngrc.bam" "override"
	END
	ACTION_IF !(FILE_EXISTS_IN_GAME "gtdrkbls.bam") BEGIN
		COPY "cdtweaks\bam\gtdrkbls.bam" "override"
	END
	//
	WITH_SCOPE BEGIN
		LAF "ADD_STATDESC_ENTRY" INT_VAR "description" = RESOLVE_STR_REF (@0) STR_VAR "bam_file" = "gtdvngrc" RET "feedback_icon_paladin" = "index" END
		LAF "ADD_STATDESC_ENTRY" INT_VAR "description" = RESOLVE_STR_REF (@1) STR_VAR "bam_file" = "gtdrkbls" RET "feedback_icon_blackguard" = "index" END
		// Listener: run 'func' each time a sprite has finished evaluating its effects
		ACTION_IF !(FILE_EXISTS_IN_GAME "m_gtlstn.lua") BEGIN
			COPY ".../cdtweaks-inlined/empty" "override\m_gtlstn.lua"
				DELETE_BYTES 0x0 BUFFER_LENGTH
				INSERT_BYTES 0x0 STRING_LENGTH "-- Listeners --%WNL%%WNL%"
				WRITE_ASCII 0x0 "-- Listeners --%WNL%%WNL%"
			BUT_ONLY_IF_IT_CHANGES
		END
		COPY_EXISTING "m_gtlstn.lua" "override"
			APPEND_FILE_EVALUATE TEXT "cdtweaks\luke\lua\divine_grace.lua"
			APPEND_FILE_EVALUATE TEXT "cdtweaks\luke\lua\dark_blessing.lua"
		BUT_ONLY UNLESS "cdtweaksDivineGrace"
		//
		ACTION_IF !(FILE_EXISTS_IN_GAME "m_gttbls.lua") BEGIN
			COPY "cdtweaks\luke\lua\m_gttbls.lua" "override"
		END
	END
END