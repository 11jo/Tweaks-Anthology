ACTION_IF game_is_pst THEN BEGIN // pst but not pstee

  ACTION_IF stack > 255 BEGIN OUTER_SET stack = 255 END
  
END

INCLUDE ~cdtweaks/lib/regexp_itm.tpa~ // build regexp array if it doesn't exist

ACTION_PHP_EACH cd_itm_type AS item => type BEGIN
  
  ACTION_IF (
             ($cd_itm_stack("%item%") > 1) AND     // if item can already stack...
             (stack > $cd_itm_stack("%item%")) AND // and the stack size would increase, and is of the type...
             (
               (type =  5) OR // arrows,
               (type = 14) OR // bullets,
               (type = 16) OR // dagger
               (type = 21) OR // hammer (iwd2)
               (type = 24) OR // darts,
               (type = 25) OR // axe,
               (type = 29) OR // spears,
               (type = 31)    // bolts,
             ) AND (
               (!game_includes_pst) OR // check all files if not pst
               ("%item%" STRING_COMPARE_REGEXP "^[Bb][Oo][Ll][Tt][0-9]+$" = 0) // only check boltXXX files on pst
             )  
           ) BEGIN

ACTION_IF regexp_debug BEGIN PRINT ~ == check passed, working on %item%~ END
           
    COPY_EXISTING ~%item%.itm~ ~override~
      SET $cd_itm_stack("%item%") = stack
      WRITE_SHORT 0x38 $cd_itm_stack("%item%")
      BUT_ONLY
      
  END

END  