DEFINE_ACTION_FUNCTION "REVISED_ARCHER"
BEGIN
	WITH_SCOPE BEGIN
		<<<<<<<< .../cdtweaks-inlined/empty
		>>>>>>>>
		ACTION_IF !(FILE_EXISTS_IN_GAME "m_gt#402.lua") BEGIN
			COPY ".../cdtweaks-inlined/empty" "override\m_gt#402.lua"
				DELETE_BYTES 0x0 BUFFER_LENGTH
				INSERT_BYTES 0x0 STRING_LENGTH "-- Functions to be invoked via op402 --%WNL%%WNL%"
				WRITE_ASCII 0x0 "-- Functions to be invoked via op402 --%WNL%%WNL%"
			BUT_ONLY_IF_IT_CHANGES
		END
		COPY_EXISTING "m_gt#402.lua" "override"
			APPEND_FILE TEXT "cdtweaks\luke\lua\revised_archer.lua"
		BUT_ONLY UNLESS "^function GTCLDSHT"
		// Listener: run 'func' each time a sprite has finished evaluating its effects
		ACTION_IF !(FILE_EXISTS_IN_GAME "m_gtlstn.lua") BEGIN
			COPY ".../cdtweaks-inlined/empty" "override\m_gtlstn.lua"
				DELETE_BYTES 0x0 BUFFER_LENGTH
				INSERT_BYTES 0x0 STRING_LENGTH "-- Listeners --%WNL%%WNL%"
				WRITE_ASCII 0x0 "-- Listeners --%WNL%%WNL%"
			BUT_ONLY_IF_IT_CHANGES
		END
		COPY_EXISTING "m_gtlstn.lua" "override"
			APPEND_FILE TEXT "cdtweaks\luke\lua\revised_archer_listener.lua"
		BUT_ONLY UNLESS "cdtweaksArcherBonusApplied"
	END
	//
	WITH_SCOPE BEGIN
		COPY_EXISTING "kitlist.2da" "override"
			COUNT_2DA_COLS "cols"
			READ_2DA_ENTRIES_NOW "read_kitlist" "%cols%"
			FOR ("i" = 0 ; "%i%" < "%read_kitlist%" ; "i" += 1) BEGIN
				READ_2DA_ENTRY_FORMER "read_kitlist" "%i%" 1 "kitname"
				PATCH_IF ("%kitname%" STR_EQ "FERALAN") BEGIN
					READ_2DA_ENTRY_FORMER "read_kitlist" "%i%" 5 "clabfile"
					READ_2DA_ENTRY_FORMER "read_kitlist" "%i%" 6 "weapprof_column_index"
					SET_2DA_ENTRY "%i%" 4 "%cols%" RESOLVE_STR_REF (@0) // update description
					SET "i" = "%read_kitlist%" // kill FOR-loop
				END
			END
		BUT_ONLY
		//
		WITH_SCOPE BEGIN
			COPY_EXISTING "%clabfile%.2da" "override"
				PATCH_IF (GAME_IS "bgee bg2ee eet") BEGIN
					REPLACE_TEXTUALLY CASE_INSENSITIVE EXACT_MATCH "AP_SPCL122" "****"
				END ELSE BEGIN
					REPLACE_TEXTUALLY CASE_INSENSITIVE EXACT_MATCH "AP_SPCL124" "****"
				END
				// formatting
				PRETTY_PRINT_2DA
			BUT_ONLY
		END
		// May only Specialize (two slots) in crossbows
		WITH_SCOPE BEGIN
			COPY_EXISTING "weapprof.2da" "override"
				COUNT_2DA_COLS "cols"
				READ_2DA_ENTRIES_NOW "read_weapprof" "%cols%"
				FOR ("i" = 0 ; "%i%" < "%read_weapprof%" ; "i" += 1) BEGIN
					READ_2DA_ENTRY_FORMER "read_weapprof" "%i%" 0 "profname"
					PATCH_IF ("%profname%" STR_EQ "CROSSBOW") BEGIN
						SET_2DA_ENTRY "%i%" "%weapprof_column_index%" "%cols%" 2
						SET "i" = "%read_weapprof%" // kill FOR-loop
					END
				END
			BUT_ONLY
		END
	END
	// +1 THAC0/Damage bonus with bows only!
	WITH_SCOPE BEGIN
		ACTION_IF (GAME_IS "bgee bg2ee eet") BEGIN
			COPY_EXISTING "spcl122.spl" "override\cdfrlntd.spl"
		END ELSE BEGIN
			COPY_EXISTING "spcl124.spl" "override\cdfrlntd.spl"
		END
		//
		COPY_EXISTING "cdfrlntd.spl" "override"
			LPF "DELETE_SPELL_HEADER" INT_VAR "header_type" = "-1" END // fresh start
			// level 1
			LPF "ADD_SPELL_HEADER" INT_VAR "target" = 5 END
			// level 3-18
			FOR ("i" = 3 ; "%i%" <= 18 ; "i" += 3) BEGIN
				LPF "ADD_SPELL_HEADER" INT_VAR "target" = 5 "required_level" = "%i%" END
				//
				LPF "ADD_SPELL_EFFECT" INT_VAR "opcode" = 321 "target" = 1 "timing" = 1 "header" = SHORT_AT 0x68 STR_VAR "resource" = "%DEST_RES%" END // Remove effects by resource
				LPF "ADD_SPELL_EFFECT" INT_VAR "opcode" = 167 "target" = 1 "timing" = 9 "header" = SHORT_AT 0x68 "parameter1" = ("%i%" / 3) END // Missile THAC0 bonus
				LPF "ADD_SPELL_EFFECT" INT_VAR "opcode" = 286 "target" = 1 "timing" = 9 "header" = SHORT_AT 0x68 "parameter1" = ("%i%" / 3) END // Missile weapon damage bonus
			END
			// level 23-33
			FOR ("i" = 23 ; "%i%" <= 33 ; "i" += 5) BEGIN
				LPF "ADD_SPELL_HEADER" INT_VAR "target" = 5 "required_level" = "%i%" END
				//
				LPF "ADD_SPELL_EFFECT" INT_VAR "opcode" = 321 "target" = 1 "timing" = 1 "header" = SHORT_AT 0x68 STR_VAR "resource" = "%DEST_RES%" END // Remove effects by resource
				LPF "ADD_SPELL_EFFECT" INT_VAR "opcode" = 167 "target" = 1 "timing" = 9 "header" = SHORT_AT 0x68 "parameter1" = ("%i%" / 5) + 3 END // Missile THAC0 bonus
				LPF "ADD_SPELL_EFFECT" INT_VAR "opcode" = 286 "target" = 1 "timing" = 9 "header" = SHORT_AT 0x68 "parameter1" = ("%i%" / 5) + 3 END // Missile weapon damage bonus
			END
		BUT_ONLY
	END
	// Called Shot (bows only!)
	WITH_SCOPE BEGIN
		WITH_SCOPE BEGIN
			COPY_EXISTING "spcl121.spl" "override\cdcl121.spl"
				WRITE_LONG NAME1 "-1" // blank name
				WRITE_LONG NAME2 "-1"
				WRITE_LONG UNIDENTIFIED_DESC "-1" // blank description
				WRITE_LONG DESC "-1"
				WRITE_SHORT 0x1C 4 // innate
				WRITE_LONG 0x34 1 // level
				//
				LPF "DELETE_EFFECT" END // fresh start
				//
				LPF "ADD_SPELL_EFFECT" INT_VAR "opcode" = 54 "target" = 2 "parameter1" = "-1" "duration" = 20 END // Base THAC0 bonus
				//
				LPF "ADD_SPELL_EFFECT" INT_VAR "header" = 2 "opcode" = 37 "target" = 2 "parameter1" = "-1" "duration" = 15 END // Save vs. spell bonus
				LPF "ADD_SPELL_EFFECT" INT_VAR "header" = 3 "opcode" = 37 "target" = 2 "parameter1" = "-1" "duration" = 15 END // Save vs. spell bonus
				LPF "ADD_SPELL_EFFECT" INT_VAR "header" = 4 "opcode" = 37 "target" = 2 "parameter1" = "-1" "duration" = 15 END // Save vs. spell bonus
				//
				LPF "ADD_SPELL_EFFECT" INT_VAR "header" = 3 "opcode" = 44 "target" = 2 "parameter1" = "-1" "duration" = 10 END // Strength bonus
				LPF "ADD_SPELL_EFFECT" INT_VAR "header" = 4 "opcode" = 44 "target" = 2 "parameter1" = "-1" "duration" = 10 END // Strength bonus
				//
				LPF "ADD_SPELL_EFFECT" INT_VAR "header" = 4 "opcode" = 12 "target" = 2 "parameter1" = 2 "parameter2" = IDS_OF_SYMBOL ("dmgtype" "missile") "timing" = 1 END // +2 (missile) Damage
			BUT_ONLY
		END
		//
		WITH_SCOPE BEGIN
			COPY_EXISTING "spcl121.spl" "override"
				GET_OFFSET_ARRAY "ab_array" SPL_V10_HEADERS
				PHP_EACH "ab_array" AS "ab_ind" => "ab_off" BEGIN
					PATCH_IF SHORT_AT ("%ab_off%" + 0x10) > 1 BEGIN
						WRITE_BYTE "%ab_off%" 0xFF // mark for later deletion
					END
				END
				LPF "DELETE_SPELL_HEADER" INT_VAR "header_type" = 0xFF END // enable deletion
				//
				LPF "ALTER_EFFECT" INT_VAR "match_opcode" = 249 STR_VAR "resource" = "cdcl121" END // Ranged hit effect
			BUT_ONLY
		END
		//
		WITH_SCOPE BEGIN
			CREATE "eff" "cdcl121"
			COPY_EXISTING "cdcl121.eff" "override"
				WRITE_LONG 0x10 402 // Invoke Lua
				WRITE_LONG 0x14 1 // Self
				WRITE_SHORT 0x2C 100 // prob1
				WRITE_ASCII 0x30 "GTCLDSHT" #8 // Lua function
			BUT_ONLY
		END
	END
	// Point Blank Shot (bows only!)
	WITH_SCOPE BEGIN
		COPY_EXISTING "x-clserg.2da" "override"
			COUNT_2DA_COLS "cols"
			READ_2DA_ENTRIES_NOW "read_x-clserg" "%cols%"
			FOR ("i" = 0 ; "%i%" < "%read_x-clserg%" ; "i" += 1) BEGIN
				READ_2DA_ENTRY_FORMER "read_x-clserg" "%i%" 0 "kitName"
				PATCH_IF ("%kitName%" STRING_EQUAL_CASE "FERALAN") BEGIN
					SET_2DA_ENTRY "%i%" 1 "%cols%" 1 // BOW
					SET "i" = "%read_x-clserg%" // kill FOR-loop
				END
			END
			PRETTY_PRINT_2DA
		BUT_ONLY
	END
END