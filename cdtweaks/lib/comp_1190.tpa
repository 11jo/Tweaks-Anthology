/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Ease of Use Romance Fixes                        \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

COMPILE ~cdtweaks/dlg/jagalvar.d~

// Anomen's title change; effects should be permanent and persist through resurrection
COPY_EXISTING ~spin678.spl~ ~override~
  READ_LONG  0x64 "abil_off"
  READ_SHORT 0x68 "abil_num"
  READ_LONG  0x6a "fx_off"
  WHILE ("%abil_num%" > 0) BEGIN
    SET "abil_num" = ("%abil_num%" - 1)
    READ_SHORT ("%abil_off%" + (0x28 * "%abil_num%")) "type"
    PATCH_IF ("%type%" = 1) BEGIN // if melee
      READ_SHORT ("%abil_off%" + 0x1e + (0x28 * "%abil_num%")) "abil_fx_num"
      READ_SHORT ("%abil_off%" + 0x20 + (0x28 * "%abil_num%")) "abil_fx_idx"
      WHILE ("%abil_fx_num%" > 0) BEGIN
        SET "abil_fx_num" = ("%abil_fx_num%" - 1)
        WRITE_BYTE ("%fx_off%" + 0x0c + (0x30 * ("%abil_fx_idx%" + "%abil_fx_num%"))) 1 // instant/permanent
      END
    END
  END
  BUT_ONLY

COPY_EXISTING ~aerie.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~SetGlobal("HaerDalisRomanceActive","GLOBAL",3)~
      ~SetGlobal("HaerDalisRomanceActive","GLOBAL",3)
      END

      IF
        InParty("HaerDalis")
        !Dead("HaerDalis")
        Global("HaerDalisRomanceActive","GLOBAL",2)
        Global("AerieRomanceActive","GLOBAL",1)
      THEN
        RESPONSE #100
          SetGlobal("HaerDalisRomanceActive","GLOBAL",1)
          Continue()
      END

      IF
        InParty("HaerDalis")
        !Dead("HaerDalis")
        !Global("HaerDalisRomanceActive","GLOBAL",0)
        !Global("HaerDalisRomanceActive","GLOBAL",3)
        Global("AerieRomanceActive","GLOBAL",2)
      THEN
        RESPONSE #100
          SetGlobal("HaerDalisRomanceActive","GLOBAL",3)
          Continue()~
    REPLACE_TEXTUALLY ~AreaType(0)~ ~AreaType(OUTDOOR)~
  END
EXTEND_TOP ~aerie.bcs~ ~cdtweaks/baf/romfix_aerie.baf~

COPY_EXISTING ~anomen.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~SetGlobal("AnomenIsNotKnight","GLOBAL",1)~
      ~SetGlobal("AnomenIsNotKnight","GLOBAL",1)
      ChangeAlignment("Anomen",CHAOTIC_NEUTRAL)~
    REPLACE_TEXTUALLY ~"TALKEDTOCOR","GLOBAL"~ ~"TALKEDCOR","GLOBAL"~
  END
EXTEND_TOP ~anomen.bcs~ ~cdtweaks/baf/romfix_anomen.baf~

COPY_EXISTING ~jaheira.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~GlobalLT("DerminSpawn","GLOBAL",5)~
      ~  GlobalLT("DerminSpawn","GLOBAL",5)
        !InParty(Myself)
      THEN
        RESPONSE #100
          RealSetGlobalTimer("JaheiraRomance","GLOBAL",3600)
          SetGlobal("DerminSpawn","GLOBAL",5)
          SetGlobalTimer("DerminAppear","GLOBAL",17280)
          StartDialogueNoSet([PC])
      END

      IF
        False()~
    REPLACE_TEXTUALLY ~\bSetGlobalTimer("TerminselAppear","GLOBAL",FIVE_DAYS)~
                      ~RealSetGlobalTimer("TerminselAppear","GLOBAL",3600)~ // originally 36000 seems to be more than random.
  END

COPY_EXISTING ~viconia.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~Global("LoveTalk","LOCALS",71)~ ~False()~
    REPLACE_TEXTUALLY ~Global("ViconiaKeldornFight","GLOBAL",1)~
      ~Global("ViconiaKeldornFight","GLOBAL",1)
      THEN
        RESPONSE #100
          SetGlobal("ViconiaKeldornFight","GLOBAL",0)
      END

      IF
        False()~
  END

// If ToB installed, patch ToB BCS files:
ACTION_IF game_is_tob THEN BEGIN

  COPY_EXISTING ~anom25.bcs~ ~override~
    DECOMPILE_AND_PATCH BEGIN
      REPLACE_TEXTUALLY ~Global("AnomenSummoned","GLOBAL",1)~
      ~Global("AnomenSummoned","GLOBAL",1)
      !Global("AnomenIsKnight","GLOBAL",1)~
    END
  EXTEND_TOP ~anom25.bcs~ ~cdtweaks/baf/romfix_anom25.baf~

  COPY_EXISTING ~jahe25.bcs~ ~override~
    DECOMPILE_AND_PATCH BEGIN
      REPLACE_TEXTUALLY ~OR(2)~ ~~
      REPLACE_TEXTUALLY ~GlobalGT("LoveTalk","LOCALS",50)~ ~~
      REPLACE_TEXTUALLY ~SetGlobal("JaheiraRomanceActive","GLOBAL",2)~ ~~
    END

END
